start : (figure)+
figure : "fig" fig_name "{" stmt_list "}"

fig_name : ID

attr_list : "{" attr ("," attr)* "}"
attr : key ":" value

key : ID
value : ATTR_VALUE
ATTR_VALUE : ESCAPED_STRING|REAL

stmt_list : (stmt)+

stmt :  size_stmt
       | subplot_stmt
       | plotting_stmt 
       | range_stmt
       | function_stmt

size_stmt : "size <-" nrows ["," ncols] [attr_list]

nrows : INT
ncols : INT 

range_stmt : independent_variable "<-" (points | range)

range : [range_descriptor] from "to" end

from : REAL
end : REAL

range_descriptor : INT "in" | "continuous" 
points : "[" point ("," point)* "]"

point : REAL

subplot_stmt : "subplot" [xindex ["," yindex]] "{" subplot_stmt_list "}"

xindex : INT
yindex : INT

subplot_stmt_list : (subplot_single_stmt)+

subplot_single_stmt : plotting_stmt 
			    | range_stmt
			    | function_stmt


plotting_stmt : dependent_variable "<-" "func" independent_variable [attr_list]

dependent_variable : ID
independent_variable : ID

function_stmt : dependent_variable "<-" (single_function | function_list)

single_function : function

function : FUNCTION

function_list : "[" function ("," function)* "]"

FUNCTION : (LCASE_LETTER|REAL|PAREN|OPERATOR)+

PAREN : "(" | ")"
OPERATOR : "+" | "-" | "*" | "/" 

ID : (LCASE_LETTER)+

_STRING_INNER: /.*?/
_STRING_ESC_INNER: _STRING_INNER /(?<!\\)(\\\\)*?/ 

ESCAPED_STRING : ("\"" _STRING_ESC_INNER "\"") 
				|("'" _STRING_ESC_INNER "'")
				|("(" _STRING_ESC_INNER ")")

%import common.LCASE_LETTER
%import common.SIGNED_NUMBER -> REAL
%import common.INT -> INT
%import common.WS
%ignore WS
