start 			: (figure)+
figure 			: "fig" fig_name "{" stmt_list "}"
fig_name		: ID

stmt_list 		: (stmt)*
stmt 			: size_stmt
       			| range_stmt
       			| function_stmt
       			| plotting_stmt 
       			| subplot_stmt


size_stmt 		: "size" "<-" nrows ["," ncols] [attr_list]
				| "size" "<-" "(" nrows "," ncols ")" [attr_list]
nrows 			: INT
ncols 			: INT 

range_stmt 		: independent_variable "<-" (points | range)
range 			: from "to" end
				| "(" from "to" end ")"
				| range_desc from "to" end
				| range_desc "(" from "to" end ")"
				| "(" range_desc from "to" end ")"
				| "(" range_desc "(" from "to" end ")" ")"
from 			: REAL
end 			: REAL
range_desc 		: INT "in" | "continuous" 
points 			: "[" number_point ("," number_point)* "]"
				| "[" string_point ("," string_point)* "]"
number_point	: REAL
string_point	: ESCAPED_STRING



dependent_variable 		: ID
independent_variable 	: ID
transcendental_func 	: ID
function_stmt 			: dependent_variable "<-" (function_expr | function_list)
function_list 			: "[" function_expr ("," function_expr)* "]"
function_expr 			: function_expr OPERATOR function_expr 
						| "(" function_expr ")"
						| transcendental_func "(" function_expr ")"
						| REAL
						| independent_variable


plotting_stmt 		: dependent_variable "<-" "func" independent_variable [attr_list]
subplot_stmt 		: "subplot" [xindex ["," yindex]] "{" subplot_stmt_list "}"
					| "subplot" "(" xindex "," yindex ")" "{" subplot_stmt_list "}"
xindex 				: INT
yindex 				: INT
subplot_stmt_list 	: (subplot_single_stmt)+
subplot_single_stmt : plotting_stmt 
			    	| range_stmt
			    	| function_stmt


attr_list 		: "{" attr ("," attr)* "}"
attr 			: key ":" value
key 			: ID
value 			: ATTR_VALUE
ATTR_VALUE 		: ESCAPED_STRING | REAL
OPERATOR 		: "+" | "-" | "*" | "/" | "^" | "%"
ID 				: CNAME
ESCAPED_STRING 	: ("\"" _STRING_ESC_INNER "\"")
				| ("'" _STRING_ESC_INNER "'")
				| ("(" _STRING_ESC_INNER ")")
COMMENT			: 	"#" /.*/



LCASE_LETTER    : "a".."z"
UCASE_LETTER    : "A".."Z"

LETTER          : UCASE_LETTER | LCASE_LETTER

CNAME           : ("_"|LETTER) ("_"|LETTER|DIGIT)*

DIGIT           : "0".."9"

INT             : DIGIT+
SIGNED_INT      : ["+"|"-"] INT
DECIMAL         : INT "." INT? | "." INT

_EXP            : ("e"|"E") SIGNED_INT
FLOAT           : INT _EXP | DECIMAL _EXP?

NUMBER          : FLOAT | INT
REAL		    : ["+"|"-"] NUMBER 


_STRING_INNER       : /.*?/
_STRING_ESC_INNER   : _STRING_INNER /(?<!\\)(\\\\)*?/


WS: /[ \t\f\r\n]/+

%ignore WS
%ignore COMMENT
